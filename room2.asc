// room script file

function hBathroomDoorBack_Interact(Hotspot *theHotspot, CursorMode mode)
{
  cEgo.Walk(194, 517,  eBlock);
  cEgo.FaceDirection(eDirectionUp, eBlock);
  Wait(5);
  cEgo.ChangeRoom(1,  325,  518,  eDirectionRight);
}

function room_FirstLoad()
{
  Room.SetProperty("lighted", false);
  Wait(20);
  cEgo.Say("&19 So dark.");
  cEgo.Say("&20 I'm so poor I can't afford light in here.");
}

function roomUpdate()
{
  if (Room.GetProperty("lighted")==true) {
    SetBackgroundFrame(1);
    region[1].Enabled = false;
    region[2].Enabled = false;
    hWashingMachine.Name = "Torture device";
  }
}

function room_Load()
{
  roomUpdate();
}

function hWashingMachine_Interact(Hotspot *theHotspot, CursorMode mode)
{
  if (Room.GetProperty("lighted")==false) {
    cEgo.Say("&21 I'm too afraid to touch this in the dark...");
    cEgo.Say("&22 I should find some source of light.");
  } else {
    cEgo.Walk(500,  517,  eBlock);
    cEgo.FaceDirection(eDirectionRight);
    cEgo.Animate(10, 5, eOnce, eBlock);
    aClick_clack.Play();
    hWashingMachine.Enabled = false;
    oWashingMachineOpen.Visible = true;
    oKey.Visible = true;
    cEgo.Animate(10, 5, eOnce, eBlock, eBackwards);
  }
}

function oKey_PickUp(Object *theObject, CursorMode mode)
{
  cEgo.Walk(568, 510,  eBlock);
  cEgo.Say("&26 Ah, a safe spot to store my spare house key.", eBlock);
  cEgo.Animate(12, 5, eOnce, eBlock);
  oKey.Visible = false;
  player.AddInventory(iKey);
  aTim_tirim_tim_tim.Play();
  cEgo.Animate(12, 5, eOnce, eBlock, eBackwards);
}

function hWallSocket_UseInv(Hotspot *theHotspot, CursorMode mode)
{
  cEgo.Walk(250, 516,  eBlock);
  if (player.ActiveInventory==iBulb) {
    cEgo.Animate(12, 5, eOnce, eBlock);
    cEgo.LoseInventory(iBulb);
    Room.SetProperty("lighted", true);
    roomUpdate();
    aElectric.Play();
    cEgo.Animate(12, 5, eOnce, eBlock, eBackwards);
  }
}

function hBathroomDoorBack_Look(Hotspot *theHotspot, CursorMode mode)
{
  cEgo.Say("&23 Way out, obviously.");
}

function hWallSocket_Look(Hotspot *theHotspot, CursorMode mode)
{
  cEgo.Say("&24 Source of all power.");
  cEgo.Say("&25 Maybe I should plug something in?");
}

function oKey_Look(Object *theObject, CursorMode mode)
{
  cEgo.Say("&26 Ah, a safe spot to store my spare house key.");
}

function oWashingMachineOpen_Look(Object *theObject, CursorMode mode)
{
  cEgo.Say("&27 This is an ordinary torture device you can find in every home!");
}

function hWashingMachine_Look(Hotspot *theHotspot, CursorMode mode)
{
  if (Room.GetProperty("lighted")==false) {
    cEgo.Say("&21 I'm too afraid to touch this in the dark...");
    cEgo.Say("&22 I should find some source of light.");
  } else {
    cEgo.Say("&27 This is an ordinary torture device you can find in every home!");
  }
}

function room_Unload()
{
  aSmash_vibe_vibe.Play();
}

function hWallSocket_Interact(Hotspot *theHotspot, CursorMode mode)
{
  cEgo.Say("&24 Source of all power.");
  cEgo.Say("&25 Maybe I should plug something in?");
}
