// room script file

function hBathroomDoor_Interact(Hotspot *theHotspot, CursorMode mode)
{
  cEgo.Walk(325, 517,  eBlock);
  cEgo.FaceDirection(eDirectionUp, eBlock);
  Wait(5);
  cEgo.ChangeRoom(2,  177,  518,  eDirectionRight);
}

function hBathroomDoor_Look(Hotspot *theHotspot, CursorMode mode)
{
   cEgo.Say("&5 This is a door leading to the bathroom.");
}

function hExitDoor_UseInv(Hotspot *theHotspot, CursorMode mode)
{
  if (player.ActiveInventory==iKey) {
    cEgo.Walk(160, 514,  eBlock);
    cEgo.FaceDirection(eDirectionDown);
    player.LoseInventory(iKey);
    cEgo.Say("&6 Good bye!");
    cEgo.FaceDirection(eDirectionUp);
    
    gCredits.Visible = true;
    int trans = 100;
    int vol = 0;
    music = aSoundtrack.Play();
    while (trans > 0) {
      trans -= 2;
      gCredits.Transparency = trans;
      vol += 1;
      music.Volume = vol;
      Wait(1);
    }
  } else {
    cEgo.Say("&7 This is the exit door. It's locked!");
  }
}

function hMedikit_Interact(Hotspot *theHotspot, CursorMode mode)
{
  cEgo.Walk(540, 511, eBlock);
  cEgo.FaceObject(oPlaster, eBlock);
  cEgo.Animate(9, 5, eOnce, eBlock);
  aClick_clack.Play();
  oOpenMedikit.Visible = true;
  oPlaster.Visible = true;
  theHotspot.Enabled = false;
  cEgo.Animate(9, 5, eOnce, eBlock, eBackwards);
}

function oPlaster_Interact(Object *theObject, CursorMode mode)
{

}

function oPlaster_PickUp(Object *theObject, CursorMode mode)
{
  cEgo.FaceObject(oPlaster,  eBlock);
  cEgo.Walk(520, 511,  eBlock);
  cEgo.Say("&2 Lampshade Fixing Device.",  eBlock);
  cEgo.Animate(11, 5, eOnce, eBlock);
  oPlaster.Visible = false;
  player.AddInventory(iPlaster);
  aTim_tirim_tim_tim.Play();
  cEgo.Animate(11, 5, eOnce, eBlock, eBackwards);
}

function hLampshade_UseInv(Hotspot *theHotspot, CursorMode mode)
{
  cEgo.FaceObject(oBulb,  eBlock);
  if (player.ActiveInventory==iPlaster) {
    cEgo.Walk(646, 416, eBlock);
    cEgo.FaceDirection(eDirectionLeft);
    cEgo.Animate(9, 5, eOnce, eBlock);
    aSwoosh_swoosh.Play();
    cEgo.LoseInventory(iPlaster);
    oLampshadeFixed.Visible = true;
    cEgo.Animate(9, 5, eOnce, eBlock, eBackwards);
    cEgo.Say("&12 So satisfying. I can leave now!");
    
    Wait(20);
    oBulb.Visible = true;
    oBulb.Move(500, 508,  20, eBlock,  eAnywhere);
    aClink.Play();
    Wait(20);
    cEgo.FaceObject(oBulb,  eBlock);
    cEgo.Say("&3 Oh, my spare lightbulb!");
    cEgo.Say("&4 I put it in the lampshade to find it easily!");
  } else {
    cEgo.Say("&8 Oh no! The lampshade is broken.");
    cEgo.Say("&9 I should fix it before leaving.");
  }
}

function oBulb_PickUp(Object *theObject, CursorMode mode)
{
  cEgo.Walk(580, 511,  eBlock);
  cEgo.FaceDirection(eDirectionLeft,  eBlock);
  cEgo.Animate(13, 5, eOnce, eBlock);
  oBulb.Visible = false;
  player.AddInventory(iBulb);
  aTim_tirim_tim_tim.Play();
  cEgo.Animate(13, 5, eOnce, eBlock, eBackwards);
}

function hExitDoor_Look(Hotspot *theHotspot, CursorMode mode)
{
  cEgo.Say("&7 This is the exit door. It's locked!");
}

function hLampshade_Look(Hotspot *theHotspot, CursorMode mode)
{
  cEgo.FaceObject(oBulb, eBlock);
  cEgo.Say("&8 Oh no! The lampshade is broken.");
  cEgo.Say("&9 I should fix it before leaving.");
}

function hMedikit_Look(Hotspot *theHotspot, CursorMode mode)
{
  cEgo.FaceObject(oPlaster, eBlock);
  cEgo.Say("&11 I bet this cabinet contains something useful.");
}

function oPlaster_Look(Object *theObject, CursorMode mode)
{
  cEgo.FaceObject(oPlaster, eBlock);
  cEgo.Say("&2 Lampshade Fixing Device.");
}

function oLampshadeFixed_Look(Object *theObject, CursorMode mode)
{
  cEgo.Say("&12 So satisfying. I can leave now!");
}

function oBulb_Look(Object *theObject, CursorMode mode)
{
  cEgo.Say("&3 Oh, my spare lightbulb!");
  cEgo.Say("&4 I put it in the lampshade to find it easily!");
}

function room_FirstLoad()
{

}

function hExitDoor_Interact(Hotspot *theHotspot, CursorMode mode)
{
  cEgo.Say("&18 I need to find my spare key!");
}

function hLampshade_Interact(Hotspot *theHotspot, CursorMode mode)
{
  cEgo.Say("&8 Oh no! The lampshade is broken.");
  cEgo.Say("&9 I should fix it before leaving.");
}

function room_Load()
{
  
}

function room_Unload()
{
  aSmash_vibe_vibe.Play();
}
